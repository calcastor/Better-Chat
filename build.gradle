//file:noinspection GroovyAssignabilityCheck
buildscript {
    def mcVersion
    def (major, minor, patch) = project.name.tokenize('-')[0].tokenize('.')
    mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://maven.minecraftforge.net/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.sk1er.club/repository/maven-public" }
    }

    dependencies {
        classpath('com.github.ReplayMod:ForgeGradle:' + (
                mcVersion >= 11200
                        ? '717a9f4' // 2.3 (1.12)
                        : 'ceb83c0' // 2.1 (1.8)
        ) + ':all')
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.replaymod.preprocess'

mcVersion = project.ext.mcVersion
def mcVersionStr = "${(int) (mcVersion / 10000)}.${(int) (mcVersion / 100) % 100}" + (mcVersion % 100 == 0 ? '' : ".${mcVersion % 100}")

preprocess {
    vars.put("MC", mcVersion)
}

version = "1.5"
group= "com.llamalad7.betterchat"
archivesBaseName = "betterchat"

kotlin.jvmToolchain {
    languageVersion = JavaLanguageVersion.of(8)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

minecraft {
    version = [
            11202: '1.12.2-14.23.0.2486',
            10809: '1.8.9-11.15.1.2318-1.8.9'
    ][mcVersion]
    mappings = [
            11202: "snapshot_20170615",
            10809: "stable_22"
    ][mcVersion]

    runDir = "run"
    makeObfSourceJar = false
    clientRunArgs += ["--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker", "--mixin betterchat.mixins.json"]
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed "gg.essential:loader-launchwrapper:1.1.2"
    implementation "gg.essential:essential-${mcVersionStr}-forge:1576+release-launch"

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    compileOnly 'org.spongepowered:mixin:0.7.11-SNAPSHOT'

    // Mixin needs these (and depends on them but for some reason that's not enough. FG, did you do that?)
    annotationProcessor 'com.google.code.gson:gson:2.2.4'
    annotationProcessor 'com.google.guava:guava:21.0'
    annotationProcessor 'org.ow2.asm:asm-tree:6.2'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.0-beta9'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    filesMatching('mcmod.info') {
        // replace version and mcversion
        expand (
                'version': project.version,
                'mcversion': project.minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

ext {
    mixinSrg = new File(project.buildDir, 'tmp/mixins/mixins.srg')
    mixinRefMap = new File(project.buildDir, 'tmp/mixins/mixins.betterchat.refmap.json')
}

compileJava {
    options.compilerArgs += [
            "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
            "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}",
            "-AreobfSrgFile=${project.file('build/mcp-srg.srg').canonicalPath}"
    ]
}

task copySrg(type: Copy, dependsOn: 'genSrgs') {
    from { project.tasks.genSrgs.mcpToSrg }
    into 'build'
}
compileJava.dependsOn copySrg

reobfJar.addSecondarySrgFile project.mixinSrg

jar {
    dependsOn configurations.embed
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) })
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    from files(project.mixinRefMap.canonicalPath)
    manifest {
        attributes 'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker',
                'MixinConfigs': 'betterchat.mixins.json',
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true
    }
}
